title: Setup
---
<<declare $optionsTimer = 0>>
<<declare $apathy = 0>>
<<declare $truth = 0>>
<<declare $killer = 0>>
===
title: newTestNode
---
<<FadeIn "Game Manager" 1>>
When does it all end?

<<set $optionsTimer to 6>>
-> bro what the fuck are you saying #delay:1 #duration:4 #vPos:top #hPos:right
    <<jump testBruh>>
-> sounds like you need a drink. #delay:2 #duration:3 #vPos:bottom #hPos:left
    <<jump testDrink>>
-> default
    <<jump testBruh>>
===

title: testBruh
---
I dunno.
===

title: testDrink
---
Yeah.
===

title: StartNode
---
<<FadeIn "Game Manager" 1>>
<<PlayLoop "Game Manager" "CLOCK_TICK">>
<<ChangeFont "Game Manager" "death">>
<<wait 2>>
<<PlayAnim "Game Manager" "DeathHands" "click-light">>
<<wait 1.5>>
<<PlaySFX "Game Manager" "LIGHT_CLICK_NOBUZZ">>
<<LightMode "Game Manager" true>>
<<PlayLoop "Game Manager" "BUZZ_LOOP">>
<<wait 1.7>>
<<PlayAnim "Game Manager" "DeathHands" "idle">>


<<wait 1>>
<<CameraInput "Game Manager" true>>

<<set $optionsTimer to 10>>
-> What is happening? #delay:0.5 #duration:8 #vPos:top #hPos:left
    <<jump explain>>
-> Where am I? #delay:2 #duration:7 #vPos:top #hPos:right
    <<jump explain>>
-> default
    <<jump speak>>
===

title: speak
---
<<PlaySFX "Game Manager" "SPEAK_DEATH">>
<<wait 0.5>>
Speak.
<<PlayAnim "Game Manager" "DeathHands" "impatient" true 2>>

<<set $optionsTimer to 11>>
-> What is happening? #delay:0.5 #duration:9 #vPos:center #hPos:right
    <<jump explain>>
-> Where am I? #delay:0.5 #duration:8 #vPos:top #hPos:left
    <<jump explain>>
-> default
    <<jump explain>>
===

title: explain
---
<<PlayAnim "Game Manager" "DeathHands" "pen tap" true 2>>
<<PlaySFX "Game Manager" "EXPLAIN_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
You are at an interview, to see if you are a good fit.

<<set $optionsTimer to 10>>
-> What if I'm not? #delay:2 #duration:6 #vPos:top #hPos:left
    <<jump whatIfImNot>>
-> A job? But I already have a job. #delay:1 #duration:8 #vPos:top #hPos:right
    <<jump alreadyHaveAJob>>
-> default 
    <<jump form>>
===

title: whatIfImNot
---
<<PlaySFX "Game Manager" "SHAME_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 1.5>>
That would be a shame.
<<wait 1>>
<<jump form>>
===

title: alreadyHaveAJob
---
<<PlaySFX "Game Manager" "NOT_ANYMORE_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 1.5>>
Not anymore.
<<wait 1>>
<<jump form>>
===

title: form
---
<<PlaySFX "Game Manager" "FIVE_MINS_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 2.5>>
You have five minutes to fill this out,
I suggest you tell the truth.
<<wait 1>>

<<set $optionsTimer to 9>>
-> What if I don't fill this out? #delay:2 #duration:6 #vPos:top #hPos:right
    <<jump whatIfIDont>>
-> Who are you? #delay:3 #duration:3 #vPos:bottom #hPos:left
    <<jump whoAreYou>>
-> default
    <<jump strengths>>
===

title: whatIfIDont
---
<<PlayAnim "Game Manager" "DeathHands" "impatient" true 2>>
<<PlaySFX "Game Manager" "PAGE_EMPTY_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
Then the page stays empty.
<<wait 1>>
<<jump strengths>>
===

title: whoAreYou
---
<<PlayAnim "Game Manager" "DeathHands" "impatient" true 2>>
<<PlaySFX "Game Manager" "NOT_IMPORTANT_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 2.5>>
That's not important.
<<wait 1>>
<<jump strengths>>
===

title: strengths
---
<<PlaySFX "Game Manager" "STRENGTHS_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
So, what's your greatest strength?
<<PlayAnim "Game Manager" "DeathHands" "writing" true 2>>

<<set $optionsTimer to 12>>
-> I'm great at washing the dishes. #delay:0.5 #duration:8 #vPos:top #hPos:right
    <<jump unsuccessful>>
-> I don't have any and don't want any. #delay:1 #duration:9 #vPos:top #hPos:left // apathy + 1
    <<set $apathy to $apathy + 1>>
    <<jump unsuccessful>>
-> I am a team player. #delay:0.5 #duration:8 #vPos:bottom #hPos:right
    <<jump unsuccessful>>
-> I am really good at customer service. #delay:3 #duration:2 #vPos:bottom #hPos:left
    <<jump unsuccessful>>
-> default
    <<jump unsuccessful>>
===

title: unsuccessful
---
<<PlayAnim "Game Manager" "DeathHands" "writing" true 2>>
<<PlaySFX "Game Manager" "UNSUCCESSFUL_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
Why were you unsuccessful?
<<PlayAnim "Game Manager" "DeathHands" "writing" true 2>>

<<set $optionsTimer to 21>>
-> Nuh uh, I'm super successful. #delay:1.5 #duration:6 #vPos:top #hPos:left
    <<jump humanLife>>
-> I suck at life, probably. #delay:0 #duration:5.5 #vPos:top #hPos:right
    <<jump humanLife>>
-> I didn't have enough friends. #delay:2 #duration:6 #vPos:bottom #hPos:left
    <<jump noFriends>>
-> Don't know, don't care. #delay:1.3 #duration:5.7 #vPos:bottom #hPos:right // apathy + 1
    <<set $apathy to $apathy + 1>>
    <<jump humanLife>>
-> I could've been better to the people who loved me... #delay:9 #duration:6 #vPos:center #hPos:left // truth + 1
    <<set $truth to $truth + 1>>
    <<jump humanLife>>
-> I didn't plan far enough ahead. #delay:10 #duration:7 #vPos:center #hPos:right // killer + 1
    <<set $killer to $killer + 1>>
    <<jump humanLife>>
-> default
    <<jump humanLife>>
===

title: noFriends
---
<<PlayAnim "Game Manager" "DeathHands" "pen tap" true 2>>
<<PlaySFX "Game Manager" "ELABORATE_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 2>>
Elaborate.
<<PlayAnim "Game Manager" "DeathHands" "writing" true 4>>

<<set $optionsTimer to 20>>
// topL
-> I don't know, but it's expected, right?. #delay:0 #duration:6 #vPos:top #hPos:left // apathy 1
    <<set $apathy to $apathy + 1>>
    <<jump humanLife>>
//topR
-> Being a working adult is lonely, man. #delay:1.5 #duration:6 #vPos:top #hPos:right
    <<jump humanLife>>
-> I could've had more connections. #delay:10.5 #duration:6 #vPos:top #hPos:right
    <<jump humanLife>>
//botL
-> I had a lot, but I didn't treasure them. #delay:2 #duration:6 #vPos:bottom #hPos:left // truth 1
    <<set $truth to $truth + 1>>
    <<jump humanLife>>
//midL
-> It's easier to get away with things. #delay:11 #duration:6 #vPos:center #hPos:left // killer 1
    <<set $killer to $killer + 1>>
    <<jump humanLife>>
//botR
-> I think friends are cool. #delay:0.5 #duration:6 #vPos:bottom #hPos:right
    <<jump humanLife>>
-> I think I missed out. #delay:9.5 #duration:6 #vPos:bottom #hPos:right
    <<jump humanLife>>
-> default
    <<jump humanLife>>
===

title: humanLife
---
<<PlayAnim "Game Manager" "DeathHands" "impatient" true 5>>
<<PlaySFX "Game Manager" "HUMAN_LIFE_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 5>>
What is the value of a human life?

<<set $optionsTimer to 15>>
// topL
-> Their net worth!. #delay:0 #duration:3 #vPos:top #hPos:left
    <<jump hell>>
-> It's too large to measure. #delay:5 #duration:4 #vPos:top #hPos:left // truth 2
    <<set $truth to $truth + 2>>
    <<jump hell>>
//topR
-> Isn't that you job to determine?. #delay:0.5 #duration:3 #vPos:top #hPos:right
    <<jump hell>>
//botL
-> How much people cry when they're gone. #delay:0 #duration:3 #vPos:bottom #hPos:left // killer 1
    <<set $killer to $killer + 1>>
    <<jump hell>>
//botR
-> The amount of good deeds they've done. #delay:0.5 #duration:3 #vPos:bottom #hPos:right
    <<jump hell>>
//midL
-> How happy they've made other people. #delay:3.5 #duration:3 #vPos:center #hPos:left // truth 1
    <<set $truth to $truth + 1>>
    <<jump hell>>
//midR
-> Nothing, humans are worthless. #delay:4 #duration:3 #vPos:center #hPos:right // apathy 1
    <<set $apathy to $apathy + 1>>
    <<jump hell>>
-> Did they have life insurance? #delay:7 #duration:2 #vPos:center #hPos:right // killer 1
    <<set $killer to $killer + 1>>
    <<jump hell>>
-> default
    <<jump hell>>
===

title: hell
---
<<PlayAnim "Game Manager" "DeathHands" "writing" true 4>>
<<PlaySFX "Game Manager" "GO_TO_HELL_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 6>>
If you were told you were going to hell, how would you feel?

<<set $optionsTimer to 15>>
//topL
-> -What?. #delay:0 #duration:9 #vPos:top #hPos:left
    <<jump pointCalc>>
//topR
-> Am I? #delay:1 #duration:3 #vPos:top #hPos:right
    <<jump pointCalc>>
//botL
-> Pretty scared, I think? #delay:2 #duration:3 #vPos:bottom #hPos:left
    <<jump pointCalc>>
//botR
-> Doesn't matter. #delay:1.5 #duration:3 #vPos:bottom #hPos:right // apathy 1
    <<set $apathy to $apathy + 1>>
    <<jump pointCalc>>
//midL
-> It would be worth it. #delay:4.5 #duration:3 #vPos:center #hPos:left // killer 1
    <<set $killer to $killer + 1>>
    <<jump pointCalc>>
//midR
-> I would understand. #delay:4.5 #duration:3 #vPos:center #hPos:right // truth 1
    <<set $truth to $truth + 1>>
    <<jump wouldUnderstand>>
-> default
    <<jump pointCalc>>
===

title: wouldUnderstand
---
<<PlayAnim "Game Manager" "DeathHands" "pen tap" true 2>>
<<PlaySFX "Game Manager" "REGRET_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
Do you regret your life?

<<set $optionsTimer to 8>>
-> Yes, all of it. #delay:0 #duration:3 #vPos:top #hPos:right
    <<jump pointCalc>>
-> No, but I regret the hurt I inflicted. #delay:3 #duration:3 #vPos:center #hPos:right
    <<set $truth to $truth + 1>>
    <<jump pointCalc>>
-> No, I regret nothing. #delay:3 #duration:3 #vPos:center #hPos:left
    <<set $killer to $killer + 1>>
    <<jump pointCalc>>
-> default
    <<jump pointCalc>>
===

title: pointCalc
---
<<PlayAnim "Game Manager" "DeathHands" "pen tap" true 4>>
//At this point, the player should have answered 6 questions.

//You can earn, in total, 4 apathy points OR 
//6 serial killer points OR 
//6 truth points 

//If the player has the 4 apathy points, they die painfully.
//<<jump apathyEnding>>
//If they have 1 or less apathy points, they proceed. 
//<<jump rememberThem>>
//If they don't fit these criteriums, they just go to hell.
//<<jump mediocrePerformanceEnding>>
...
<<if $apathy == 4>>
    <<jump apathyEnding>>
<<endif>>

<<if $apathy <= 1>>
    <<jump rememberThem>>
<<endif>>

<<jump mediocrePerformanceEnding>>
===

title: rememberThem
---
<<PlaySFX "Game Manager" "REMEMBER_THEM_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
Do you remember them?

<<set $optionsTimer to 5>>
-> Yes. #delay:0 #duration:3 #vPos:top #hPos:left
    <<jump yesIRemember>>
-> No. #delay:0 #duration:3 #vPos:top #hPos:right
    <<jump noIForgor>>
-> default
    <<jump dontWantRemember>>
===

title: yesIRemember
---
<<set $optionsTimer to 6>>
-> How could I not? #delay:0 #duration:3 #vPos:top #hPos:left
    <<jump confessionCalc>>
-> I don't want to remember... #delay:2 #duration:3 #vPos:bottom #hPos:right
    <<jump dontWantRemember>>
-> default
    <<jump dontWantRemember>>
===

title: noIForgor
---
<<PlayAnim "Game Manager" "DeathHands" "impatient" true 3>>
<<PlaySFX "Game Manager" "ARE_YOU_CERTAIN_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
Are you certain?

<<set $optionsTimer to 7>>
-> Yes, I'm sure. #delay:0 #duration:3 #vPos:top #hPos:right
    <<jump mediocrePerformanceEnding>>
-> I remember their screams. #delay:3 #duration:3 #vPos:top #hPos:left // killer 1
    <<set $killer to $killer + 1>>
    <<jump confessionCalc>>
-> I dont want to remember... #delay:2 #duration:3 #vPos:bottom #hPos:right
    <<jump dontWantRemember>>
-> default
    <<jump dontWantRemember>>
===

title: confessionCalc
---

//See which number is bigger, serial killer or truth.
//If the player has a clear winner in points, jump to the appropriate one.
//If it is tied, show them both.
<<set $optionsTimer to 60>>
// truth 
<<DialogueOption "Game Manager" "They were my greatest regrets." "truthConfession" 0 3 "mid" "L">>
-> They were my greatest regrets. <<if $truth >= $killer>> #delay:0 #duration:5 #vPos:center #hPos:left
    <<jump truthConfession>>

// serial killer
//<<DialogueOption "Game Manager" "I chose them to send to you." "serialKillerConfession" 0 3 "mid" "R">>
-> I chose them to send to you. <<if $killer >= $truth>> #delay:0 #duration:5 #vPos:center #hPos:right
    <<jump serialKillerConfession>>
-> default
    <<jump serialKillerConfession>>
===

title: dontWantRemember
---
<<PlayAnim "Game Manager" "DeathHands" "pen tap" true 3>>

<<ChangeFont "Game Manager" "player">>
player: But I... do remember. 

<<set $optionsTimer to 8>>
-> Please don't make me... #delay:0 #duration:6 #vPos:center #hPos:left
    <<jump mediocrePerformanceEnding>>
-> It's the right thing to do. #delay:3 #duration:3 #vPos:center #hPos:right
    <<set $truth to $truth + 1>>
    <<jump confessionCalc>>
-> default
    <<jump mediocrePerformanceEnding>>
===

title: truthConfession
---

<<ChangeFont "Game Manager" "player">>
player: My whole life was a series of mistakes,
player: one after the other.
player: What I did to them was the biggest mistake of all,
player: trading their lives for a few extra minutes.
player: But I don't regret it.
player: To live is to make mistakes in this shitty, irrational, beautiful world,
player: in my worthless, precious, beautiful years of being alive. 
player: I'm at peace with hell, even if I am never forgiven. 
player: And,
player: whether I've earned forgiveness is not up to me to decide.
player: ...
player: Nor you, for that matter.

<<set $optionsTimer to 4>>
-> But I hope you can accept my application #delay:0 #duration:3 #vPos:bottom #hPos:right
    <<jump deathFunnie>>
-> default 
    <<jump trueEndingP1>>
===

title: deathFunnie
---

<<ChangeFont "Game Manager" "player">>
player: I think being tortured by immortality would hurt less than seeing the people I hurt again.
<<ChangeFont "Game Manager" "death">>
<<PlaySFX "Game Manager" "LAUGH_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
HA!!
<<jump trueEndingP1>>
===

title: trueEndingP1
---

// you got the job yay!
<<ChangeFont "Game Manager" "death">>
<<PlaySFX "Game Manager" "PAINFUL_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 2>>
This...
will be painful.
<<PlaySFX "Game Manager" "NO_REST_DEATH">>
<<wait 0.5>>
You will not rest.
<<PlaySFX "Game Manager" "LISTEN_DEATH">>
<<wait 0.5>>
You will listen.
<<PlaySFX "Game Manager" "REMEMBER_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
You will remember.

<<set $optionsTimer to 60>>
-> And I will feel... everything #delay:0 #duration:3 #vPos:center #hPos:center
    <<jump trueEndingP2>>
-> default
    <<jump trueEndingP2>>
===

title: trueEndingP2
---
...
<<wait 1>>
<<PlayAnim "Game Manager" "DeathHands" "shake hands" true>>
<<FadeOut "Game Manager" 1>>
===

title: mediocrePerformanceEnding
---
//stormguy doing this
//The ending where you fall into the void.
<<ChangeFont "Game Manager" "death">>
<<PlaySFX "Game Manager" "INTERVIEW_OVER_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
The interview is over, 
<<PlaySFX "Game Manager" "THANK_YOU_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 2>>
thank you for your time.
<<PlaySFX "Game Manager" "ENJOY_NEXT_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 7>>
I'd tell you to enjoy what comes next, but... you won't.
<<FadeToScene "Game Manager" "hell-fall">>
===

title: apathyEnding
---
<<ChangeFont "Game Manager" "death">>
...
<<PlaySFX "Game Manager" "APATHY_ENDING_MONOLOGUE_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 2>>
// have to manually set advance delays later
I was like you once. 
So dismissive of your own existence. 
Doesn't it get tiring?
Feigning non-chalance?
Going through your former life without making a sound louder than a whisper,
feeling nothing more than numbing misery. 
...
Here,
let me give you a feeling worth screaming for.

<<PlayAnim "Game Manager" "DeathHands" "rotting hands" true >>

<<wait 10>>
<<FadeOut "Game Manager" 3>>
===

title: serialKillerConfession
---

<<ChangeFont "Game Manager" "player">>
player: I hand picked them myself.
player: Young...
player: bright...
player: People with plenty to live for...
player: ...and plenty of life to live.
player: Did you like... how they taste?
<<ChangeFont "Game Manager" "death">>
...
<<wait 2>>
<<ChangeFont "Game Manager" "player">>
player: Why aren't you saying anything...?
player: I did this for you...

<<ChangeFont "Game Manager" "death">>
<<PlaySFX "Game Manager" "SPECIAL_DEATH">>
<<wait 0.5>>
<<SetAdvanceDelay "Game Manager" 3>>
Do you think you're special?
<<wait 1>>
You deserve this.
<<wait 3>>
<<jump serialKillerEnding>>
===

title: serialKillerEnding
---
<<FadeToScene "Game Manager" "Snowy">>
// scene transition
===